/* This file is part of SableCC ( http://sablecc.org ).
 *
 * See the NOTICE file distributed with this work for copyright information.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

Language lexer_features;

Lexer

  c_comment = Shortest '/*' Any* '*/';

  eol = #13 | #10 | #13 #10;

  line_comment = '//' (Shortest (Any* Look (eol | '' Look End))) (eol | '' Look End);

  letter = upper_case | lower_case;
  upper_case = 'A'..'Z';
  lower_case = 'a'..'z';
  digit = '0'..'9';

  if_keyword = 'if';
  while_keyword = 'while';

  identifier = letter (letter | digit)*;
  number = digit+;

  ip_address = (number Separator '.')^4;

  hex_digit = 'a'..'f' | upper_case - ('G'..'Z') | digit;
  hex = '0x' hex_digit+;

  zero = '0'+ | 'zero';

  Token
    zero, if_keyword, 'else', while_keyword, identifier, number, ip_address, hex;

  Ignored
    c_comment, line_comment, ' ', #10, #xD;

  Priority
    zero > identifier;
    zero > number;
