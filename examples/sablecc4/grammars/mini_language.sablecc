/* This file is part of SableCC ( http://sablecc.org ).
 *
 * See the NOTICE file distributed with this work for copyright information.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

Grammar mini_language;

Lexer

  id = letter (letter | digit)*;
  number = digit+;

  letter = 'a'..'z';
  digit = '0'..'9';

  string_text = string_char+;
  string_char = (#32..#126) - ('"' | '\\');

  blank = (' ' | tab | vtab | eol)+;
  tab = #9;
  vtab = #11;
  eol = cr | lf | cr lf;
  cr = #13;
  lf = #10;

  ctrl_z = #26;

  line_comment = Shortest('//' Any* Longest(eol | End));

  comment_text = Any+ - ('/*' | '*/');

Parser

  Ignored
    blank, comment, line_comment;

  program =
    statement? ctrl_z?;

  statement =
    {block:} '{' declaration* statement* '}' |
    {assign:} id '=' expression ';' |
    {if:} 'if' '(' expression ')' statement else? |
    {while:} 'while' '(' expression ')' statement |
    {print:} 'print' '(' (expression Separator ',')* ')' ';';

  Dangling else =
    'else' statement;

  declaration =
    type (id Separator ',')+ ';';

  type =
    {int:} 'int' |
    {bool:} 'bool' |
    {string:} 'string';

  expression =
    {eq:} [left:]sub_expression '==' [right:]sub_expression |
    {neq:} [left:]sub_expression '!=' [right:]sub_expression |
    {lt:} [left:]sub_expression '<' [right:]sub_expression |
    {gt:} [left:]sub_expression '>' [right:]sub_expression |
    {lteq:} [left:]sub_expression '<=' [right:]sub_expression |
    {gteq:} [left:]sub_expression '>=' [right:]sub_expression |
    {simple:} sub_expression;

  sub_expression =
    {add:} [left:]sub_expression '+' [right:] sub_expression |
    {sub:} [left:]sub_expression '-' [right:] sub_expression |
    {mul:} [left:]sub_expression '*' [right:] sub_expression |
    {div:} [left:]sub_expression '/' [right:] sub_expression |
    {mod:} [left:]sub_expression '%' [right:] sub_expression |
    {not:} '!' sub_expression |
    {par:} '(' expression ')' |
    {number:} number |
    {true:} 'true' |
    {false:} 'false' |
    {string:} string |
    {read:} 'read' '(' ')' |
    {str2int:} 'str2int' '(' expression ')';

    Precedence
      Unary not;
      Left mul, div, mod;
      Left add, sub;

Context string;

  Token string =
    '"' string_part* '"';

  string_part =
    {string_text:} string_text |
    {string_escape:} string_escape;

Context escape;

  string_escape =
    {back_slash:} '\\' '\\' |
    {double_quote:} '\\' '"' |
    {new_line:} '\\' 'n' |
    {tab:} '\\' 't';

Context comment;

  Token comment =
    '/*' comment_part* '*/';

  comment_part =
    {comment_text:} comment_text |
    {nested_comment:} comment;
